plugins {
    id 'java'
    id 'maven-publish'
    id "io.freefair.lombok" version "6.5.1"
    id "com.github.johnrengelman.shadow" version "4.0.4"
}

defaultTasks 'build'

group 'eu.decentsoftware.holograms'
version '2.7.2'
description 'Lightweight yet very powerful hologram plugin with many features and configuration options.'

repositories {
    mavenCentral()
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://repo.codemc.io/repository/nms/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://repo.dmulloy2.net/repository/public/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot:1.13-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.1'
    compileOnly 'com.arcaniax:HeadDatabase-API:1.3.1'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.8.0'
    implementation 'org.bstats:bstats-bukkit:3.0.0'

    // Annotations
    compileOnly 'org.jetbrains:annotations:23.0.0'
    annotationProcessor 'org.jetbrains:annotations:23.0.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.java
            artifactId 'decentholograms'
        }
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

build {
    dependsOn shadowJar
}

shadowJar {
    relocate "org.bstats", "eu.decentsoftware.holograms.metrics"
}